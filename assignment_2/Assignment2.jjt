options { 
    JAVA_UNICODE_ESCAPE = true;
    MULTI = true;
    VISITOR = true;
}
PARSER_BEGIN(SLPParser)
import java.util.*;
import java.io.*;

public class SLPParser {
    public static SymbolTable ST = new SymbolTable();
    //public static SymbolTable ST = new SymbolTable();
    public static void main(String args[]) throws ParseException, FileNotFoundException {
        if (args.length < 1) {
            System.out.println("Please supply a source file to compile");
            return;
        }
        SLPParser parser = new SLPParser(new FileInputStream(args[0]));
        SimpleNode root = parser.Prog();
        root.dump("");
        TypeCheckVisitor tc = new TypeCheckVisitor();
        root.jjtAccept(tc, ST);
        // System.out.println(ST.check("x"));
        // System.out.println(ST.get("y")); 
    }
}
PARSER_END(SLPParser)
/*****************************************
***** SECTION 3 - TOKEN DEFINITIONS *****
*****************************************/
TOKEN_MGR_DECLS :
{
    static int commentNesting = 0;
}
SKIP : /*** Ignoring spaces/tabs/newlines ***/
{
    " "
    | "\t"
    | "\n"
    | "\r"
    | "\f"
    | < "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
}

SKIP : /* COMMENTS */
{
    "/*" { commentNesting++; } : IN_COMMENT
}
<IN_COMMENT> SKIP :
{
    "/*" { commentNesting++; }
    | "*/" { commentNesting--;
        if (commentNesting == 0)
        SwitchTo(DEFAULT);
    }
    | <~[]>
}
TOKEN : /* Keywords and punctuation */
{
    < SEMIC : ";" >
    | < COLON : ":" >
    | < ASSIGN : ":=" >
    | < PRINT : "print" >
    | < LBR : "(" >
    | < RBR : ")" >
    | < COMMA : "," >
    | < PLUS_SIGN : "+" >
    | < MINUS_SIGN : "-" >
    | < DIV_SIGN : "/" >
}

TOKEN [IGNORE_CASE]: /* Keywords */
{
    < RETURN : "return" >
    | < MAIN : "main" >
    | < BEGIN : "begin" >
    | < END : "end" >
    | < VARIABLE : "variable" >
    | < CONSTANT : "constant" >
    | < TYPE : ("integer" | "boolean") >
    | < IS : "is" >
    | < BOOL : "true" | "false" >
    | < IF : "if" >
    | < ELSE : "else" >
    | < WHILE : "while" > 
    | < SKIP_WORD : "skip" > 
}

TOKEN: /* Logical Operators */
{
    < LOG_NEG : "~" > // logical negation
    | < LOG_DIS : "|" > // logical disjunction
    | < LOG_AND : "&" > // logical and
}

TOKEN: /* Comparison Operators */
{
    < EQUALS : "=" > // logical equals
    | < NOT_EQUALS : "!=" > // not equals
    | < LESS_THAN : "<" > // less than
    | < LESS_EQUALS: "<=" > // less than or equals 
    | < GREATER_THAN: ">" > // greater than
    | < GREATER_EQUALS: ">=" > // greater than or equals
}

TOKEN : /* Numbers and identifiers */
{
    < NUM : ("-")*(<DIGIT>)+ > // num can start with a minus
    | < #DIGIT : ["0" - "9"] >
    | < ID : <LETTER> (<LETTER> | <DIGIT>)* >
    | < #LETTER : ["a" - "z", "A" - "Z", "_"] >
}

TOKEN : /* Anything not recognised so far */
{
    < OTHER : ~[] >
}
/***********************************
***** SECTION 4 - THE GRAMMAR *****
***********************************/

SimpleNode Prog() #PROGRAM : {}
{
    code() <EOF> {return jjtThis;}
}

void code() #code : {}
{
    (decl_list())*
    (func_list())*
    main_statement()
}

void decl_list() #void : {}
{
    (decl() <SEMIC> [decl_list() #decl_list(2)])
}

void decl() : {}
{
    var_decl()  
    | const_decl()
}

/** DECLARATION **/
void var_decl() #void : { Token t; String name; }
{
    <VARIABLE> name = id() <COLON> t = <TYPE>
        {
            jjtThis.value = t.image;
            System.out.println(name + " is added to the SymbolTable");
            ST.insert(name, t.image); 
            //ST.insert(name);
        } #VAR(1)
}

void const_decl() #void : { Token t; String name; }
{
    <CONSTANT> name = id() <COLON> t = <TYPE>
        {
            jjtThis.value = t.image;
            System.out.println(name + " is added to the SymbolTable");
            ST.insert(name, t.image);
            //ST.insert(name);
        } #CONST(1)
}

/** FUNCTION **/
void func_list() #void : {}
{
    {ST.newScope();}
    (func() [func_list()])
    {ST.endScope();}
}

void func() : {}
{
        <TYPE> <ID> <LBR> (param_list())* <RBR> <IS> (decl_list())* (statement_block())*
}

void param_list() : {}
{
    (nemp_param_list())
}

void nemp_param_list() #void : {}
{   
    param() [<COMMA> nemp_param_list() #nemp_param_list(2)]
}

void param() : {Token t; String name;} 
{
    name = id() <COLON> t = <TYPE>
        {
            jjtThis.value = t.image;
            ST.insert(name, t.image); 
            //ST.insert(name);
        } #VAR(1)
}

/** CONDITION **/
void condition() : {}   
{
    simple_condition() (log_ops() simple_condition())*
}

void simple_condition() : {}
{
    <LOG_NEG> condition()
    | <LBR> condition() <RBR>
    //| expr() comp_ops() expr()
}

void comp_ops() : {} 
{
    <EQUALS>
    | <NOT_EQUALS>
    | <LESS_THAN>
    | <LESS_EQUALS>
    | <GREATER_THAN>
    | <GREATER_EQUALS>
}

void log_ops() : {}
{
    <LOG_DIS>
    | <LOG_AND>
}

/** MAIN STATEMENT **/
void main_statement() : {} 
{
    {ST.newScope();}
    <MAIN> <BEGIN> (decl_list())* (statement_block())* <END>
    {ST.endScope();}
}

void statement_block() #void : {}
{
    (statement() [statement_block() #statement_block(2)])
}

void statement() : {}
{
    LOOKAHEAD(2) assign()
    | LOOKAHEAD(2) func_call()
    | <BEGIN> (statement_block())* return_statement() <END>
    | <IF> condition() <BEGIN> (statement_block())* <END>
    | <ELSE> <BEGIN> (statement_block()) * <END>
    | <WHILE> condition() <BEGIN> (statement_block())* <END>
    | <SKIP_WORD> <SEMIC>
}

void assign() : {String name;}
{
    (name = id() <ASSIGN> expr() <SEMIC>)
}

void func_call() : {String name;}
{
    name = id() <LBR> (expr())* <RBR> <SEMIC>
}

void expr() : {}
{
    LOOKAHEAD(3) fragment() bin_op() fragment()
    //| LOOKAHEAD(2) (id())* <LBR> expr() <RBR>
    | arg_list()
    | fragment()
}

void arg_list() : {}
{
    nemp_arg_list()
}

void nemp_arg_list() #void : {String name;}
{
    (name = id() [<COMMA> nemp_arg_list() #nemp_arg_list(2)])
    {
        ST.insertArgs(name); 
    }
}

void bin_op() : {}
{
    <PLUS_SIGN> 
    | <MINUS_SIGN>
}

String fragment() : { String name; Token t; }
{
    name = id()
    | t = <MINUS_SIGN> name = id() {jjtThis.value = t.image; return t.image;}
    | t = <NUM> {jjtThis.value = t.image; return t.image;}
    | t = <BOOL> {jjtThis.value = t.image; return t.image;}
}

/** SIMPLE TOKENS **/
String id()  : { Token t; }
{
    t = <ID> {jjtThis.value = t.image; return t.image;}
}

void return_statement() : {} 
{ 
    <RETURN> <LBR> (expr())* <RBR> <SEMIC>
}
